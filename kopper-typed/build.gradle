apply plugin: 'kotlin'
apply plugin: 'java'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

dependencies {
    compile project(':kopper')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.

    testCompile 'org.testng:testng:6.8.1',
            "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
}

test {
    useTestNG()
    testLogging {
        // showStandardStreams = true
        exceptionFormat "short"
        events "PASSED", "FAILED", "SKIPPED"

        debug {
            events "STARTED", "FAILED", "SKIPPED"
            exceptionFormat "full"
        }
    }
}

publishing {
    repositories {
        maven {
            name 'SonatypeStaging'
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username "$ossrhUsername"
                password "$ossrhPassword"
            }
        }
        maven {
            name 'SonatypeReleases'
            url "https://oss.sonatype.org/content/groups/public"
            credentials {
                username "$ossrhUsername"
                password "$ossrhPassword"
            }
        }
        maven {
            name 'SonatypeSnapshots'
            url "https://oss.sonatype.org/content/repositories/snapshots"
            credentials {
                username "$ossrhUsername"
                password "$ossrhPassword"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId "$groupId"
            version "$version-$buildNumber"

            from components.java

            pom.withXml { xmlProvider ->
                def project = xmlProvider.asNode()

                project.appendNode('name', 'Kopper Typed')

                project.appendNode('description', 'Typed extensions for kopper')

                project.appendNode('url', 'https://github.com/jimschubert/kopper')

                def developers = project.appendNode('developers')
                def developer = developers.appendNode('developer')
                developer.appendNode('id', 'jimschubert')
                developer.appendNode('name', 'Jim Schubert')
                developer.appendNode('email', 'james.schubert@gmail.com')

                def licenses = project.appendNode('licenses')
                def license = licenses.appendNode('license')
                license.appendNode('name', 'MIT')
                license.appendNode('url', 'https://github.com/jimschubert/kopper/blob/master/LICENSE')
            }
        }
    }
}